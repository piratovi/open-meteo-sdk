plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.1'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'io.freefair.lombok' version "8.4"
    id 'maven-publish'
}

group = 'com.kolosov'
version = '0.0.4'

java {
    sourceCompatibility = '21'
    withJavadocJar()
    withSourcesJar()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

publishing {
    publications {
        myS3RepoPublication(MavenPublication) { // Choose a descriptive name
            groupId = project.group.toString()
            artifactId = project.name
            version = project.version.toString()
            from components.java
        }
    }
    repositories {
        maven {
            name = "myS3Repo"
            url = "s3://piratovi-artifacts"
            credentials(AwsCredentials) {
                accessKey = System.getenv('AWS_ACCESS_KEY_AOL')
                secretKey = System.getenv('AWS_ACCESS_SECRET_AOL')
            }
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

bootJar {
    enabled = false // We don't need a separate executable JAR for this module
}

jar {
    enabled = true // Enable standard JAR creation for this module
}
