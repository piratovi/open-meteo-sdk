plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'maven-publish'
}

group = 'com.kolosov'
version = '0.0.8'

ext {
    mapstructVersion = '1.6.3'
    lombokMapstructBindingVersion = '0.2.0'
    lombokVersion='1.18.38'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
    withJavadocJar()
    withSourcesJar()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

publishing {
    publications {
        myS3RepoPublication(MavenPublication) { // Choose a descriptive name
            groupId = project.group.toString()
            artifactId = project.name
            version = project.version.toString()
            from components.java
        }
    }
    repositories {
        maven {
            name = "myS3Repo"
            url = "s3://piratovi-artifacts"
            credentials(AwsCredentials) {
                accessKey = System.getenv('AWS_ACCESS_KEY_AOL')
                secretKey = System.getenv('AWS_ACCESS_SECRET_AOL')
            }
        }
    }
}

dependencies {
    // Use lightweight Spring Web + Boot autoconfigure for library
    implementation 'org.springframework.boot:spring-boot-autoconfigure'
    implementation 'org.springframework:spring-web'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    // MapStruct
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    // Spring Boot configuration metadata for @ConfigurationProperties (optional but helpful)
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // Lombok for test scope
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    // Make sure MapStruct processor runs after Lombok
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"
    testAnnotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

bootJar {
    enabled = false // We don't need a separate executable JAR for this module
}

jar {
    enabled = true // Enable standard JAR creation for this module
}
